evaluationDependsOn(':subModules:ServiceTestAppPojos')

def versionMajor = 2
def versionMinor = 1
def versionPatch = 19
def versionBuild = 0

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

apply plugin: 'android'
apply plugin: 'crashlytics'


repositories {
    mavenCentral()
    maven { url 'http://download.crashlytics.com/maven' }
}

dependencies {

    compile project(':subModules:ServiceTestAppPojos')

    compile 'com.google.guava:guava:15.+'
    // compile 'com.google.code.gson:gson:2.2.+'
    // compile 'joda-time:joda-time:2.3'
    compile 'com.jakewharton:butterknife:4.0.+'
    compile 'com.jakewharton.timber:timber:2.2.+'
    // compile 'com.github.frankiesardo:icepick:2.3.+'
    // provided 'com.github.frankiesardo:icepick-processor:2.3.+'
    compile 'com.squareup:otto:1.3.+'
    compile 'nl.qbusict:cupboard:2.0.0'

    compile 'com.squareup.okhttp:okhttp:1.3.0'
    compile 'com.squareup.picasso:picasso:2.2.+'
    compile 'com.squareup.retrofit:retrofit:1.5.+'
    debugCompile 'com.squareup.retrofit:retrofit-mock:1.4.1'
    // compile 'com.koushikdutta.ion:ion:1.2.+'

    compile 'com.crashlytics.android:crashlytics:1.+'

    compile 'com.android.support:support-v13:18.0.+'
    compile 'com.google.android.gms:play-services:3.1.+'
    androidTestCompile files('libs/espresso-1.1-bundled.jar')
}


android {
    compileSdkVersion 19
    buildToolsVersion "19"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_NUMBER", "\"${versionBuild}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            packageNameSuffix ".debug"
            versionNameSuffix ".${versionBuild}"
        }
    }
}
